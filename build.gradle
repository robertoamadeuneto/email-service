plugins {
    id "org.springframework.boot" version "2.1.5.RELEASE"
    id "com.avast.gradle.docker-compose" version "0.9.1"
    id "com.adarshr.test-logger" version "1.6.0"
    id "java"
}

apply plugin: "io.spring.dependency-management"
apply plugin: "jacoco"

group = "br.com.maxplorer"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "1.8"

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "Greenwich.SR1")
    set("lombokVersion", "1.18.6")
    set("sendGridVersion", "4.3.0")
    set("assertjVersion", "3.11.1")
    set("powerMockVersion", "2.0.2")
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.cloud:spring-cloud-stream"
    implementation "org.springframework.cloud:spring-cloud-starter-stream-rabbit"
    implementation "com.sendgrid:sendgrid-java:${sendGridVersion}"

    runtimeOnly "org.springframework.cloud:spring-cloud-stream-binder-rabbit"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.cloud:spring-cloud-stream-test-support"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powerMockVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks {
    test {
        systemProperty "spring.profiles.active", "local"
    }

    bootJar {
        archiveFileName.set("app.jar")
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check {
        dependsOn(jacocoTestReport)
    }
}

dockerCompose {
    isRequiredBy(test)
}

testlogger {
    setTheme("mocha")
}